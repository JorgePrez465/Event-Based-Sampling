'CR1000X Series
'Created by Jorge Perez (github: JorgePrez465)

' Datalogger variables
Public Batt_V
Public PanelTemp_C

' Sensors
Public Ultrasonic
Public raw_ultrasonic
Public Rain_inch
Public TDR(3)
Public BubblerData(3)

' Program Variables
Public Raining As BOOLEAN
Public RainingTimeElapsed
Public TimeSinceLastSample
Public WaterSamples As UINT1

' Bubbler Data, assign a name to each array component from SDI-12
Alias BubblerData(1) = BubblerStageInch
Alias BubblerData(2) = Line_PSI
Alias BubblerData(3) = Tank_PSI

' TDR Data array
Alias TDR(1) = VWC
Alias TDR(2) = EC
Alias TDR(3) = T

' Asigning units
Units Batt_V=Volts
Units PanelTemp_C=Deg C
Units Rain_inch=inch
Units VWC=m^3/m^3
Units EC=dS/m
Units T=Deg C
Units Line_PSI=PSI
Units Tank_PSI=PSI
Units BubblerStageInch=inch

' Private Variables 
Dim ReadyToSample As BOOLEAN 


'Define Data Tables
DataTable(Data_Table, True, -1)
	DataInterval(0, 5, Min, 10)
	Average(1, BubblerStageInch, FP2, False)
	Sample(1, BubblerStageInch ,FP2)
	Sample(1, Line_PSI, FP2)
	Average(1, VWC, FP2, False)
	Average(1, EC, FP2, False)
	Average(1, T, FP2, False)
	Totalize(1, Rain_inch, FP2, False)
	Average(1, Ultrasonic, FP2, False)
	Average(1, raw_ultrasonic, FP2, False)
	''Maximum(1, WaterSamples, FP2,False,True)
EndTable


BeginProg
	' Initialize ReadyToSample so it takes the first sample as soon as it can
	ReadyToSample = 1  

	Scan(1, Sec, 1, 0)

		Battery(Batt_V)
		PanelTemp(PanelTemp_C, 60)
		
		PulseCount(Rain_inch, 1, P1, 1, 0, 0.01, 0)
		' Every time the tipping bucket tips, Raining Variable restarts
		If Rain_inch > 0 Then 
			Timer(1, hr, 2)
			Raining = 1 ' True 
		EndIf

		' If 10 minutes from last sample have passed, take another sample
		If TimeSinceLastSample >= 10 Then
			ReadyToSample = 1
		EndIf

		' If 4 hours from last tip, raining is False and resets timer
		If RainingTimeElapsed >= 4 Then 
			Raining = 0
			WaterSamples = 0
			SW12(SW12_1, 0)
			raw_ultrasonic = 0
			Ultrasonic = 0
			Timer(1, hr, 3)
		EndIf

		' If raining is True, read ultrasonic data.
		If Raining Then
			SW12(SW12_1, 1) 
			CurrentSE(raw_ultrasonic,1, mv1000, RG2, True, 0, 60, 1, 0)
			CurrentSE(Ultrasonic, 1, mv1000, RG2, True, 0, 60, 4.6875,-14.75)	' Freq = 50Hz
		EndIf

		If TimeIntoInterval(0, 1, Hr) Then
			SDI12Recorder(TDR(), C3, "0", "M!", 1, 0, -1)
		EndIf

		If TimeIntoInterval(0, 5, sec) AND Raining Then
			SDI12Recorder(BubblerData(), C7, "0", "M!", 1, 0, -1)
		EndIf

		If BubblerStageInch >= 0.5 AND ReadyToSample Then ' For B -> 1.10
			ReadyToSample = 0
			PortSet(C2, 1)
   			Delay(0, 25 , mSec)
			PortSet(C2, 0)
			Timer(2, min, 2)
			WaterSamples = WaterSamples + 1
		EndIf

		 
		' Timer(TimerNumber, TimeUnits, TOptions: 0-Start, 1-stop, 2-reset, 3-stop&restet, 4-ReadOnly)
		TimeSinceLastSample = Timer(2, min, 4) 
		RainingTimeElapsed = Timer(1, hr, 4)

		CallTable Data_Table
	NextScan
EndProg

